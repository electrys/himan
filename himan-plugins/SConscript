#
# SConstruct for himan-plugins

Import('env')
import os
import sys

# cuda toolkit path

cuda_toolkit_path = '/usr/local/cuda-5.5'

if os.environ.get('CUDA_TOOLKIT_PATH') is None:
        print "Environment variable CUDA_TOOLKIT_PATH not set, assuming " + cuda_toolkit_path
	os.environ['CUDA_TOOLKIT_PATH'] = cuda_toolkit_path
else:
        cuda_toolkit_path = os.environ['CUDA_TOOLKIT_PATH']

have_cuda = False

if os.path.isfile(cuda_toolkit_path + '/lib64/libcudart.so'):
	have_cuda = True

# List all auxiliary plugins

auxiliary_plugins = ['cache','fetcher','grib','hitool','neons','pcuda','querydata','writer']

# List all compiled plugins that have only CPU implementation here

compiled_plugins = ['icing', 'kindex', 'precipitation', 'seaicing', 'hybrid_pressure', 'hybrid_height', 'cloud_type', 'ncl', 'rain_type', 'preform_pressure', 'relative_humidity']

# List all compiled plugins that have both CPU and GPU implementation here

cuda_plugins = ['dewpoint', 'tk2tc','tpot', 'fog', 'windvector', 'vvms']

if have_cuda:
	env.Tool('cuda')

else:
	compiled_plugins.extend(cuda_plugins)

for p in auxiliary_plugins:

	file = 'source/' + p + '.cpp'
	env.SharedLibrary(target = p, source = [file])

for p in compiled_plugins:

	file = 'source/' + p + '.cpp'
	base = 'source/compiled_plugin_base.cpp'

	files = [file, base]

	env.SharedLibrary(target = p, source = files)

if have_cuda:
	for p in cuda_plugins:

		file = 'source/' + p + '.cpp'
		base = 'source/compiled_plugin_base.cpp'
		cufile = 'source/' + p + '_cuda.cu'
 
		files = [file, base, cufile]

		env.SharedLibrary(target = p, source = files)

